/****************************************************************\
**       Métodos requeridos en los ejercicios del Tema 1        **
** Autores: Pablo Baeyens Fernández y José Manuel Muñoz Fuentes **
\****************************************************************/

load(linearalgebra)$  /* jacobian */

/* Valor absoluto o norma del máximo, según el contexto */

norma(x) := if listp(x) then lmax(abs(x)) else abs(x)$

/* Método de bisección */

biseccion(f,a,b,tol) := block(
  [old:inf, new:-inf],
  iter: 0,
  if f(a)*f(b) >= 0 then return("ERROR"),
  while norma(old-new) > tol and iter < 10^5 do (
    iter: iter+1,
    old: new,
    new: (a+b)/2,
    if f(new) = 0 then return(new),
    if f(a)*f(new) < 0 then
      b:new
    else
      a: new
  ),
  float(new)
)$


/* Método de regula falsi */

regula_falsi(f,a,b,tol) := block(
  [old:inf, new:-inf],
  iter: 0,
  if f(a)*f(b) >= 0 then return("ERROR"),
  while norma(old-new) > tol and iter < 10^5 do (
    iter: iter+1,
    old: new,
    new: b-(b-a)/(f(b)-f(a))*f(b),
    if f(new) = 0 then return(new),
    if f(a)*f(new) < 0 then
       b:new
    else
       a: new
  ),
  float(new)
)$


/* Método de la secante */

secante(f,a,b,tol) := block(
  [older,old:a, new:b],
  iter: 0,
  while norma(old-new) > tol and iter < 10^5 do (
    iter: iter+1,
    older:old,
    old: new,
    new: old-(old-older)/(f(old)-f(older))*f(old),
    if new < a or new > b then return("error: fuera de rango"),
    if f(new) = 0 then return(new)
  ),
  float(new)
)$


/* Método de Newton-Raphson */

newton_raphson(f,a,b,tol) := block(
  [old:a, new:(a+b)/2,df],
  iter: 0,
  define(df(x),diff(f(x),x)),
  while norma(old-new) > tol and iter < 10^5 do (
    iter: iter+1,
    old: new,
    new: old-f(old)/df(old),
    if new < a or new > b then return("error: fuera de rango"),
    if f(new) = 0 then return(new)
  ),
  float(new)
)$


/* Método de Euler, pedido en el ejercicio 6 */

eulerm(f,a,b,tol) := block(
  [old:a, new:(a+b)/2,df,ddf],
  iter: 0,
  define(df(x),diff(f(x),x)),
  define(ddf(x),diff(df(x),x)),
  while norma(old-new) > tol and iter < 10^5 do (
    iter: iter+1,
    old: new,
    new: old-f(old)/df(old)*2/(1 + sqrt(1 - 2*f(old)*ddf(old)/(df(old)^2))),
    if new < a or new > b then return("error: fuera de rango"),
    if f(new) = 0 then return(new)
  ),
  float(new)
)$


/* Obtiene el límite iterado de g */

itera(g,iters,tol,x0) := block(
  [old,new:x0],
  iter: 0,
  while iter < iters do (
    iter: iter+1,
    old:new,
    new:float(g(new)),
    if norma(new-old) < tol then return(float(new))
  ),
  float(new)
)$

/*  Obtiene el límite iterado de g con el método de Steffensen  */
/* Método basado en la aceleración de la convergencia de Aitken */

itera_st(g,iters,tol,x0) := block(
  [old,new:x0,gnew],
  iter: 0,
  while iter < iters do (
    iter: iter+1,
    old:new,
    gnew: g(new),
    new:float(new-(gnew-new)^2/(g(gnew) - 2*gnew + new)),
    if norma(new-old) < tol then return(float(new))
  ),
  float(new)
)$

/*  Obtiene el límite iterado de g: R^N -> R^N  */
/* Basado en el método de Gauss-Seidel para SEL */

itera_gs(g,iters,tol,x0) := block(
  [old,new:x0,i],
  iter: 0,
  while iter < iters do (
    iter: iter+1,
    old:copylist(new),
    for i:1 thru length(new) do new[i]: float(g(new)[i]),
    if norma(new-old) < tol then return(float(new))
  ),
  float(new)
)$

/* Obtiene la lista de coeficientes de un polinomio */

coeficientes(p) := block(
  [p_expandido:expand(p(x))],
  makelist(coeff(p_expandido, x, i), i, hipow(p(x),x), 0, -1)
)$

/* Algoritmo de Horner, pedido en el ejercicio 13 */
/* Devuelve una lista con p(x0) y q(x) tales que: */
/*   p(x) = (x-x0)·q(x) + p(x0)                   */

horner(p, x0) := block(
  [a,b,r,grado],
  if listp(p) then a:p else a:coeficientes(p),
  kill(b),
  grado:length(a)-1,
  if grado = 0 then return([a[1], [0]]),
  b[0]:a[1],
  b[n]:=a[n+1]+x0*b[n-1],
  r:a[grado+1]+x0*b[grado-1],
  b:listarray(b),
  [r, b]
)$

/* Método de Newton para sistemas de ecuaciones lineales */

newton_raphson_sist(f,as,bs,tol,vars,iters) := block(
  [old:as, new:(as+bs)/2,Jf,Jfit,ratprint:false],
  iter: 0,
  define(funmake(Jf, vars), jacobian(apply(f, vars), vars)), /* Jacobiana */
  while norma(old-new) > tol and iter < iters do (
    iter: iter+1,
    Jfit:transpose(invert(apply(Jf, new))),  /* Jacobiana inversa transpuesta en 0 */
    old: new,
    new: float(old-(apply(f, old).Jfit)[1]),
    if norma(apply(f, new)) = 0 then return(new)
  ),
  float(new)
)$

/* Versión que resuelve el SEL en lugar de invertir la matriz */

newton_raphson_sist2(f,as,bs,tol,vars,iters) := block(
  [old:as, new:(as+bs)/2,Jf,ratprint:false],
  iter: 0,
  define(funmake(Jf, vars), jacobian(apply(f, vars), vars)), /* Jacobiana */
  while norma(old-new) > tol and iter < iters do (
    iter: iter+1,
    old: new,
    new: old-float(map(rhs, linsolve(list_matrix_entries(apply(Jf, old).transpose(vars) - transpose(apply(f, old))), vars))),
    if norma(apply(f, new)) = 0 then return(new)
  ),
  float(new)
)$

/* Versión que usa la matriz jacobiana inicial en todas las iteraciones */

newton_raphson_sist_m(f,as,bs,tol,vars,iters) := block(
  [old:as, new:(as+bs)/2,Jf,Jfit0,ratprint:false],
  iter: 0,
  define(funmake(Jf, vars), jacobian(apply(f, vars), vars)), /* Jacobiana */
  Jfit0:transpose(invert(apply(Jf, new))),  /* Jacobiana inversa transpuesta en 0 */
  while norma(old-new) > tol and iter < iters do (
    iter: iter+1,
    old: new,
    new: float(old-(apply(f, old).Jfit0)[1]),
    if norma(apply(f, new)) = 0 then return(new)
  ),
  float(new)
)$
