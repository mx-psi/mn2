/****************************************************************\
**       Métodos requeridos en los ejercicios del Tema 2        **
** Autores: Pablo Baeyens Fernández y José Manuel Muñoz Fuentes **
\****************************************************************/

/* Diferencias divididas */

dd(f, v):=block([n:length(v),df],
    if v[1] = v[n] then
        (define(df(x), diff(f(x),x,n-1)), df(v[1])/(n-1)!)
    else (dd(f, makelist(v[i],i,1,n-1))-dd(f, makelist(v[i],i,2,n)))/(v[1]-v[n])
)$

/* Diferencias divididas de datos discretos */

dd_discreto(d):=block([n:length(d)],
  if n = 1 then d[1][2] else
  (dd_discreto(makelist(d[i], i, 1, n-1))-dd_discreto(makelist(d[i],i,2,n)))/(d[1][1]-d[n][1])
)$


/* Elemento i-ésimo de la base de Lagrange */

bli(v, i) := product(if i = j then 1 else (x-v[j])/(v[i]-v[j]), j, 1, length(v))$

/* Base de Lagrange */

bl(v) := makelist(bli(v, i), i, 1, length(v))$


/* Polinomio de interpolación mediante base de Lagrange */

pl(f, v) := sum(f(v[i])*bli(v, i), i, 1, length(v))$


/* Polinomio de interpolación mediante base de Newton */

pn(f, v) := sum(
    dd(f, 
    makelist(v[j],j,1,i))*
    product(x-v[j], j, 1, i-1)
  , i, 1, length(v)
)$

/* Polinomio de interpolación mediante base de Newton de datos discretos */

pn_discreto(d) := sum(
    dd_discreto(makelist(d[j],j,1,i))*
    product(x-d[j][1], j, 1, i-1)
  , i, 1, length(d)
)$

/* FDN de tipo interpolatorio */

fdn(f, v, c) := block(define(dp(x), diff(pl(f, v), x)), dp(c))$

/* FCN de tipo interpolatorio */

fcn(f, v, a, b) := integrate(pl(f, v), x, a, b)$

/* Polinomio i-ésimo de Legendre */

leg[0](x) := 1$
leg[i](x) := x^i - sum(integrate(x^i*leg[j](x),x,-1,1)/integrate(leg[j](x)*leg[j](x),x,-1,1)*leg[j](x),j,0,i-1)$


/* Nodos del polinomio i-ésimo de Legendre */
nodos_leg(n,a,b):= map(
lambda([x], ((b-a)*rhs(x) + a + b)/2),
realroots(leg[n](x)))$

